C
      PROGRAM OA118
C
C     ******************************************************************
C     *
C     *
C     *    ONERA--DIRECTION DE L'AERODYNAMIQUE
C     *
C     *
C     *               DIVISION OAT2
C     *
C     *
C     *
C     *    DEVELOPPED BY  T.H. LE    (PT.24-30)
C     *
C     *
C     *    VECTORISED BY  H. BOILLOT    (PT.25-38)
C     * non //
C     *
C     ******************************************************************
C
C     Modifications:
C      - in OAMAT1, common CFN is shorter than in OAVITEL; since PIPS
C     requires uniquely defined commons, it had to be extended with
C     the array AIRE(NFAC); Francois Irigoin, December 1990
C      - in OAMAT1 and OAVITEL common OO is shorter than in OA118; as
C     mentionned above, it had to be extended; Francois Irigoin, December 1990
C      - in SOLV, IF()GOTO replaced by IF THEN ENDIF; Francois Irigoin, 
C     March 1991
C      - in SOLV3, IF THEN GOTO 1 ENDIF replaced by ELSE; Francois Irigoin,
C     April 1991
C      - in PHWPAN, IF GOTO 6 replaced by IF THEN ENDIF; Francois Irigoin,
C     April 1991
C      - in PHWAK, IF GOTO 6 replaced by IF THEN ENDIF; Francois Irigoin,
C     April 1991
C      - in CLOCK, call to TIME commented out; Francois Irigoin, March 1993
C
      PARAMETER(NFAC=49,NKJ=3,NDOM=1,ALWAK=+2.,NSAU=1,
     1          ORI=+0.0,CLO=1.000,ALPHA=02.0,DERI=+0.0,SREF=1./3.,
     2            NDOW=1,DWAK=100.)
      COMMON/OASET/PIO2
      COMMON/EE/E11,E22,E33
C
      COMMON/GEF/XF(NFAC,0:4),YF(NFAC,0:4),ZF(NFAC,0:4)
      COMMON/OO/O(NFAC),VS(NFAC),PS(NFAC)
      COMMON/FX/FXX(NFAC),FXY(NFAC),FXZ(NFAC)
      COMMON/FE/FEX(NFAC),FEY(NFAC),FEZ(NFAC)
      COMMON/CFN/FNX(NFAC),FNY(NFAC),FNZ(NFAC),AIRE(NFAC)
      COMMON/PCP/XO(NFAC),YO(NFAC),ZO(NFAC)
      COMMON/OACOF/AA(NFAC,NFAC)
      COMMON/GRA/GRD(NFAC,3)
      COMMON/OT/SWX(4,NKJ),SWY(4,NKJ),SWZ(4,NKJ),OTXE(NKJ)
      COMMON/OAWAK/PHW(NFAC)
      DIMENSION IY(2),CPC(NFAC)
      DIMENSION XLE(NKJ),CD(NKJ),JEX(NKJ),JIX(NKJ)
      DIMENSION INE1(NDOW),INE2(NDOW),INI1(NDOW),INI2(NDOW)
      DIMENSION IMD(NDOM),JMD(NDOM),IFA(NDOM)
      DIMENSION R(NKJ),RL(NKJ),PSV(NKJ,NKJ)
      DIMENSION CC0(NKJ),CC(NKJ,NKJ)
      INTEGER*4 CLOCK,T1,T2,T3,T4,T


102   FORMAT('1',53X,'DONNEES GEOMETRIQUES'//22X,'I',7X,'X',7X,'Y',9X,'Z
     &',8X,'XO',7X,'YO',7X,'ZO',8X,'NX',6X,'NY',6X,'NZ'//)              0001
104   FORMAT(2X,I5,3(1X,F12.6),1X,3(1X,F12.6),1X,3(1X,F12.6))           0002
106   FORMAT(7X,3(1X,F12.6))                                            0003
600   FORMAT(E12.5,5(1X,E12.5))                                         0004
601   FORMAT(62X,'*INCIDENCE:ALPHA=*',F7.4,//)                          0005
606   FORMAT('1',30X,'RESULTATS COMPLETS DU CALCUL',//,5X,'I',7X,'X',10X
     &,'Y',10X,'Z',12X,'U',10X,'V',10X,'W',8X,'KP'/)                    0006
607   FORMAT(40X,I3,5X,4(1X,I3),5X,4(1X,I3))                            0007
608   FORMAT(1X,I5,1X,3(F12.6,1X),2X,3(F12.6,1X),2X,F12.6)              0008
609   FORMAT(/,40X,'ORIGINE DU REPERE:X=',F10.4,8X,'*LONGUEUR DE REFEREN
     &:C=',F10.4,//63X,'*NOMBRE DE FACETTES:*',I5)                      0009
610   FORMAT(1X,'*CONTROLE*',2X,I5,5(E12.5,6X))                         0010
613   FORMAT(//////64X,'*PARAMETRES DE CALCUL*',///)                    0011
700   FORMAT(2I6,8(2X,E12.5))                                           0012
731   FORMAT(2I6,8E12.5)                                                0013
732   FORMAT(3I5,8E12.5)                                                0014
733   FORMAT(1X,10(2X,I5))                                              0015
      OPEN (UNIT=2,FILE='oares')                                        0016
C
C     ******************************************************************
C     ***PARAMETRES DE CALCUL*******************************************
C     ******************************************************************
C
      T1 = CLOCK()                                                      0017
      PI = 4.*ATAN(1.)                                                  0018
      PIO2 = 2.*PI                                                      0019
      PIO4 = 4.*PI                                                      0020
      RPIO4 = 1./PIO4                                                   0021
      IY(1) = 1                                                         0022
      IY(2) = -1                                                        0023
C
      ALFA = ALPHA*PI/180.                                              0024
      BETTA = DERI*PI/180.                                              0025
      AMWAK = ALWAK*PI/180.                                             0026
      UINFI = COS(ALFA)*COS(BETTA)                                      0027
      VINFI = SIN(BETTA)*COS(ALFA)                                      0028
      WINFI = SIN(ALFA)                                                 0029
C
      INE1(1) = 43                                                      0030
      INE2(1) = 45                                                      0031
      INI1(1) = 47                                                      0032
      INI2(1) = 49                                                      0033
C
      IN = 0                                                            0034
      DO 171 N = 1, NDOW                                                0035
         IN1 = INE1(N)                                                  0036
         IN2 = INE2(N)                                                  0037
         DO 172 I = 1, IN2-IN1+1                                        0038
            IN = IN+1                                                   0039
            JEX(IN) = IN1+I-1                                           0040
172         CONTINUE                                                    0041
171      CONTINUE                                                       0042
C
      IN = 0                                                            0043
      DO 173 N = 1, NDOW                                                0044
         IN1 = INI1(N)                                                  0045
         IN2 = INI2(N)                                                  0046
         DO 174 I = 1, IN2-IN1+1                                        0047
            IN = IN+1                                                   0048
            JIX(IN) = IN2-I+1                                           0049
174         CONTINUE                                                    0050
173      CONTINUE                                                       0051
C#       WRITE(100,733)(JEX(N),N=1,NKJ)
C#       WRITE(100,733)(JIX(N),N=1,NKJ)
C
C
C     ******************************************************************
C     ***DISCRETISATION DE L OBSTACLE EN FACETTES***********************
C     ******************************************************************
C
      T1 = CLOCK()                                                      0052
      CALL OAMET                                                        0053
      T2 = CLOCK()                                                      0054
      T = T2-T1                                                         0055
      WRITE (100, *) ' OAMET :', T                                      0056
C
C#      READ(2,*) (IMD(I),I=1,NDOM)
C#      READ(2,*) (JMD(I),I=1,NDOM)
      IFA(1) = 0                                                        0057
      DO 221 N = 2, NDOM                                                0058
         IFA(N) = IFA(N-1)+IMD(N-1)*JMD(N-1)                            0059
221      CONTINUE                                                       0060
C
      WRITE (*, 102)                                                    0061
      DO 206 I = 1, NFAC                                                0062
         WRITE (100, 104) I, XF(I,1), YF(I,1), ZF(I,1), XO(I), YO(I)    0063
     &   , ZO(I), FNX(I), FNY(I), FNZ(I)                                0063
         DO 204 K = 2, 4                                                0064
            WRITE (100, 106) XF(I,K), YF(I,K), ZF(I,K)                  0065
204         CONTINUE                                                    0066
206      CONTINUE                                                       0067
C
      WRITE (100, 613)                                                  0068
      WRITE (100, 601) ALPHA                                            0069
      WRITE (100, 609) ORI, CLO, NFAC                                   0070
C
      DO 209 I = 1, NFAC                                                0071
         VS(I) = FNX(I)*UINFI+FNY(I)*VINFI+FNZ(I)*WINFI                 0072
209      CONTINUE                                                       0073
C
C
C
C DEFINITION DES NAPPES
C
      E11 = COS(AMWAK)                                                  0074
      E22 = 0.                                                          0075
      E33 = SIN(AMWAK)                                                  0076
C
      DO 175 N = 1, NKJ                                                 0077
         IP = JEX(N)                                                    0078
         IQ = JIX(N)                                                    0079
         SWX(1,N) = XF(IP,2)                                            0080
         SWX(2,N) = XF(IP,2)+DWAK*E11                                   0081
         SWX(3,N) = XF(IP,3)+DWAK*E11                                   0082
         SWX(4,N) = XF(IP,3)                                            0083
         SWY(1,N) = YF(IP,2)                                            0084
         SWY(2,N) = YF(IP,2)+DWAK*E22                                   0085
         SWY(3,N) = YF(IP,3)+DWAK*E22                                   0086
         SWY(4,N) = YF(IP,3)                                            0087
         SWZ(1,N) = ZF(IP,2)                                            0088
         SWZ(2,N) = ZF(IP,2)+DWAK*E33                                   0089
         SWZ(3,N) = ZF(IP,3)+DWAK*E33                                   0090
         SWZ(4,N) = ZF(IP,3)                                            0091
         XLE(N) = 0.5*(XF(N,1)+XF(N,4))                                 0092
         CD(N) = 0.5*(XF(IP,2)+XF(IP,3))-XLE(N)                         0093
         WRITE (100, 732) N, IP, IQ, SWX(1,N), SWX(2,N), SWY(1,N),      0094
     &   SWY(2,N), SWZ(1,N), SWZ(2,N)                                   0094
175      CONTINUE                                                       0095
C
C     ******************************************************************
C     ***COEFFICIENTS DE LA MATRICE****************************
C     ******************************************************************
C
      T2 = CLOCK()                                                      0096
C
      CALL OAMAT1                                                       0097
C
      T3 = CLOCK()                                                      0098
      T = T3-T2                                                         0099
      WRITE (100, *) ' TEMPS OAMAT1 :', T                               0100
      DO 999 I = 1, NFAC                                                0101
         WRITE (100, *) I                                               0102
C#      WRITE(100,*)(AA(I,J),J=1,NFAC)
999      CONTINUE                                                       0103

C#      WRITE(100,*)(O(I),I=1,NFAC)
C
      DO 250 I = 1, NFAC                                                0104
         PS(I) = O(I)                                                   0105
250      CONTINUE                                                       0106
C
C
C     ******************************************************************
C     ***CALCUL DE MU0**************************
C
C     ************** RESOLUTION PAR M.G. ********
      T3 = CLOCK()                                                      0107
      CALL GRAD1(PS, O)                                                 0108
      T4 = CLOCK()                                                      0109
      T = T4-T3                                                         0110
      WRITE (100, *) ' TEMPS RESOLUTION MU0 PAR M.G.= ', T              0111
C
      DO 114 I = 1, NKJ                                                 0112
         JE = JEX(I)                                                    0113
         JI = JIX(I)                                                    0114
         CC0(I) = O(JE)-O(JI)                                           0115
114      CONTINUE                                                       0116
C
C*******CALCUL DES NKJ CHAMPS CIRCULATOIRES *********************
      DO 998 J = 1, NKJ                                                 0117
C
         CALL PHWAK(J)                                                  0118
C
C
C     ******************************************************************
C     ***CALCUL DE MUJ**************************
C
C     ************** RESOLUTION PAR M.G. ********
C
         T3 = CLOCK()                                                   0119
         CALL GRAD1(PHW, O)                                             0120
         T4 = CLOCK()                                                   0121
         T = T4-T3                                                      0122
         WRITE (100, *) ' TEMPS RESOLUTION MUW', J, ' PAR M.G.= ', T    0123
C
         DO 116 I = 1, NKJ                                              0124
            JE = JEX(I)                                                 0125
            JI = JIX(I)                                                 0126
            CC(J,I) = O(JE)-O(JI)                                       0127
116         CONTINUE                                                    0128
998      CONTINUE                                                       0129
C
C  ****CALCUL MUW, K.J. CONTINUITE DES MU********
C
C
      DO 98 J = 1, NKJ                                                  0130
         DO 96 JJ = 1, NKJ                                              0131
            PSV(J,JJ) = CC(JJ,J)                                        0132
96          CONTINUE                                                    0133
98       CONTINUE                                                       0134
      DO 1098 J = 1, NKJ                                                0135
         PSV(J,J) = PSV(J,J)-1                                          0136
1098     CONTINUE                                                       0137
C
      DO 69 J = 1, NKJ                                                  0138
69       RL(J) = -CC0(J)                                                0139
C
      CALL SOLV(NKJ, PSV, RL)                                           0140
C
      DO 90 J = 1, NKJ                                                  0141
         OTXE(J) = RL(J)                                                0142
90       CONTINUE                                                       0143
C
      CALL PHWPAN                                                       0144
C
      DO 222 I = 1, NFAC                                                0145
222      PS(I) = PS(I)+PHW(I)                                           0146
C
C     ******************************************************************
C     ***CALCUL DE MU***************************
C
C     ************** RESOLUTION PAR M.G. ********
C
      T3 = CLOCK()                                                      0147
      CALL GRAD1(PS, O)                                                 0148
      T4 = CLOCK()                                                      0149
      T = T4-T3                                                         0150
      WRITE (100, *) ' TEMPS RESOLUTION MU PAR M.G.= ', T               0151
C*
      DO 349 I = 1, NKJ                                                 0152
         JE = JEX(I)                                                    0153
         JI = JIX(I)                                                    0154
         OOO = OTXE(I)-O(JE)+O(JI)                                      0155
         WRITE (100, 610) I, OTXE(I), O(JE), O(JI), OOO                 0156
349      CONTINUE                                                       0157
C
C     ******************************************************************
C     ***CALCUL DU GRADIENT DE MU******************************
C     ******************************************************************
C
      DO 886 N = 1, NDOM                                                0158
         IM = IMD(N)                                                    0159
         JM = JMD(N)                                                    0160
         IFAC0 = IFA(N)                                                 0161
         T3 = CLOCK()                                                   0162
         CALL OAVITEL(IM, JM)                                           0163
         T4 = CLOCK()                                                   0164
         T = T4-T3                                                      0165
         WRITE (100, *) ' TEMPS OAVITEL.= ', T                          0166

886      CONTINUE                                                       0167
C
C     ******************************************************************
C     ***VITESSE ET PRESSION AUX POINTS DE CONTROLE*********************
C     ******************************************************************
C
      DO 366 I = 1, NFAC                                                0168
         GRD(I,1) = -GRD(I,1)-FNX(I)*VS(I)+UINFI                        0169
         GRD(I,2) = -GRD(I,2)-FNY(I)*VS(I)+VINFI                        0170
         GRD(I,3) = -GRD(I,3)-FNZ(I)*VS(I)+WINFI                        0171
C     GRD(I,1)=-GRD(I,1)
C     GRD(I,2)=-GRD(I,2)
C     GRD(I,3)=-GRD(I,3)
         V2 = GRD(I,1)*GRD(I,1)+GRD(I,2)*GRD(I,2)+GRD(I,3)*GRD(I,3)     0172
         CPC(I) = 1.-V2                                                 0173
C     CPC(I)=SQRT(V2)
366      CONTINUE                                                       0174
C
C
      CX = 0.                                                           0175
      CY = 0.                                                           0176
      CZ = 0.                                                           0177
      DO 417 I = 1, NFAC                                                0178
C     CX=CX+CPC(I)*FNX(I)*AIRE(I)
         CX = CX+AIRE(I)                                                0179
C     CY=CY+CPC(I)*FNY(I)*AIRE(I)
         CZ = CZ+CPC(I)*FNZ(I)*AIRE(I)                                  0180
417      CONTINUE                                                       0181
      CY = CZ/SREF                                                      0182
      WRITE (100, 610) IDT, CX, CY, CZ                                  0183
      WRITE (100, 606)                                                  0184
      REWIND (11)                                                       0185
      AMACH = CY                                                        0186
      WRITE (11, 730) AMACH, ALPHA                                      0187
      DO 509 I = 1, NFAC                                                0188
         XR = XO(I)-ORI                                                 0189
         YR = YO(I)                                                     0190
         ZR = ZO(I)                                                     0191
         OR = O(I)                                                      0192
         WRITE (11, 730) XR, YR, ZR, GRD(I,1), GRD(I,2), GRD(I,3),      0193
     &   CPC(I), OR                                                     0193
         WRITE (100, 608) I, XO(I), YO(I), ZO(I), GRD(I,1), GRD(I,2)    0194
     &   , GRD(I,3), CPC(I)                                             0194
509      CONTINUE                                                       0195
      REWIND (11)                                                       0196
730   FORMAT(8F10.5)                                                    0197
      REWIND (12)                                                       0198
      JF = JMD(1)                                                       0199
      NS = (JF-NSAU)/2                                                  0200
      NPE = NFAC/JF                                                     0201
      NPI = NPE                                                         0202
      NPOL = 1                                                          0203
      AMACH = CY                                                        0204
      RE = 999.                                                         0205
      WRITE (12, 731) NS, NPOL, AMACH, ALPHA, RE, RE                    0206
C
      DO 709 J = 1, NS                                                  0207
         YR = YO(J)                                                     0208
         CLN = 0.                                                       0209
         CLA = 0.                                                       0210
         WRITE (12, 731) NPE, NPI, YR                                   0211
         DO 707 I = 1, NPE                                              0212
            IR = JF*(I-1)+J                                             0213
            XR = XO(IR)/CD(J)-XLE(J)/CD(J)                              0214
            ZR = ZO(IR)/CD(J)                                           0215
            CLNI = -CPC(IR)*FNZ(IR)*AIRE(IR)                            0216
            CLAI = CPC(IR)*FNX(IR)*AIRE(IR)                             0217
            CLN = CLN+CLNI                                              0218
            CLA = CLA+CLAI                                              0219
            WRITE (12, 731) J, I, ZR, XR, CPC(IR), O(IR)                0220
707         CONTINUE                                                    0221
         DO 708 I = 1, NPE                                              0222
            IR = JF*I-J+1                                               0223
            XR = XO(IR)/CD(J)-XLE(J)/CD(J)                              0224
            ZR = ZO(IR)/CD(J)                                           0225
            CLNI = -CPC(IR)*FNZ(IR)*AIRE(IR)                            0226
            CLAI = CPC(IR)*FNX(IR)*AIRE(IR)                             0227
            CLN = CLN+CLNI                                              0228
            CLA = CLA+CLAI                                              0229
            WRITE (12, 731) J, I, ZR, XR, CPC(IR), O(IR)                0230
708         CONTINUE                                                    0231
         CLN = CLN/(YF(J,4)-YF(J,1))/CD(J)                              0232
         CLA = CLA/(YF(J,4)-YF(J,1))/CD(J)                              0233
         CLL = CLN*UINFI-CLA*WINFI                                      0234
         WRITE (*, 104) J, YR, CLN, CLA, CLL                            0235
709      CONTINUE                                                       0236
      REWIND (12)                                                       0237
C
      STOP                                                              0238
      END
