!
! everything about packing
!
! $Id$
! $Log: hpfc_packing.m4f,v $
! Revision 1.16  1997/03/20 07:50:01  coelho
! better RCS headers.
!
!
! macros for code generation
!
define(`LIST_DECL_BOUNDS',`
     $ dl$1, du$1')dnl
define(`LIST_SECTION',`
     $ rl$1, ru$1, ri$1')dnl
define(`DO_LOOP',`
      do i$1 = rl$1, ru$1, ri$1')dnl
define(`ENDDO_LOOP',`
      enddo')dnl
define(`ARRAY_DECL',`dl$1:du$1')dnl
define(`INDEX_REF',`i$1')dnl
define(`GENERIC_PACKING',`dnl
pushdef(`LEVEL',$1)dnl
pushdef(`PVMFUNCTION',$2)dnl
pushdef(`NAME',$3)dnl
pushdef(`PVMTYPE',$4)dnl
pushdef(`TYPE',m4_type_name(PVMTYPE))dnl
!
! Packing LEVEL dimensional array
!
      subroutine build_name(HPFC,NAME,PVMTYPE,LEVEL)(
     $     array, dnl
COMMA_REPLICATE(LEVEL, `LIST_DECL_BOUNDS'),dnl
COMMA_REPLICATE(LEVEL, `LIST_SECTION')
     $)
      integer dnl
COMMA_REPLICATE(LEVEL, `LIST_DECL_BOUNDS'),dnl
COMMA_REPLICATE(LEVEL, `LIST_SECTION')
      TYPE
     $ array(COMMA_REPLICATE(LEVEL, `ARRAY_DECL'))
      include "hpfc_commons.h"
      integer info, nitem
      integer COMMA_REPLICATE(LEVEL, `INDEX_REF')
      i1=rl1
      nitem=(ru1-rl1+1)/ri1
REVERSE_BUT_LAST_REPLICATE(LEVEL, `DO_LOOP')
         call PVMFUNCTION
     $        (PVMTYPE,
     $         array(COMMA_REPLICATE(LEVEL, `INDEX_REF')),
     $         nitem, ri1, info)  dnl
SIMPLE_REPLICATE(eval(LEVEL - 1), `ENDDO_LOOP')
      end  
!
popdef(`LEVEL')dnl
popdef(`PVMFUNCTION')dnl
popdef(`NAME')dnl
popdef(`TYPE')dnl
popdef(`PVMTYPE')')dnl
!
define(ONE_PACKING,`
GENERIC_PACKING($1,pvmfpack,PACK,$2)
GENERIC_PACKING($1,pvmfunpack,UNPACK,$2)')dnl
define(ALLPACK,`
! ----------------------------------------
!
! Packing serie for pvmtype $1
!
forloop(`_D_',1,_HPFC_DIMENSIONS_,`ONE_PACKING(_D_,$1)')
!')dnl
!
ifdef(`_HPFC_NO_BYTE1_',,`ALLPACK(BYTE1)')
ifdef(`_HPFC_NO_INTEGER2_',,`ALLPACK(INTEGER2)')
ifdef(`_HPFC_NO_INTEGER4_',,`ALLPACK(INTEGER4)')
ifdef(`_HPFC_NO_REAL4_',,`ALLPACK(REAL4)')
ifdef(`_HPFC_NO_REAL8_',,`ALLPACK(REAL8)')
ifdef(`_HPFC_NO_COMPLEX8_',,`ALLPACK(COMPLEX8)')
ifdef(`_HPFC_NO_COMPLEX16_',,`ALLPACK(COMPLEX16)')
!
! that is all
!
