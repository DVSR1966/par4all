c
c $RCSfile: hpfc_broadcast.m4f,v $ (version $Revision$)
c $Date: 1995/09/11 17:36:18 $, 
c
c management of broadcasts
c typical call: hpfc_broadcast_x(lid, [number, step]*x)
c broadcast the current message to the described set of pe.
c
define(`LIST_DECL_AREA',`
     $     n$1, s$1')dnl
define(`LIST_INDEX_DECL',`
     $     i$1')dnl
define(`DO_LOOP',`
      do i$1 = i`'eval($1 - 1), i`'eval($1 - 1)+n$1*s$1, s$1')dnl
define(`ENDDO_LOOP',`
      enddo')dnl
define(`GENERIC_BROADCAST',`
pushdef(`LEVEL',$1)dnl
      subroutine build_name(hpfc_broadcast,LEVEL)(
     $     lid, dnl
COMMA_REPLICATE(LEVEL,`LIST_DECL_AREA'))
      integer
     $     lid, i0, info, dnl
COMMA_REPLICATE(LEVEL,`LIST_DECL_AREA'),dnl
COMMA_REPLICATE(LEVEL,`LIST_INDEX_DECL')
      include "fpvm3.h"
      include "hpfc_commons.h"
      i0 = lid
SIMPLE_REPLICATE(LEVEL,`DO_LOOP')
      if (i`'LEVEL.ne.MYLID) then
         call pvmfsend(NODETIDS(i`'LEVEL), 
     $        SENDCHANNELS(i`'LEVEL), info)
         SENDCHANNELS(i`'LEVEL) = SENDCHANNELS(i`'LEVEL) + 2
      endif
SIMPLE_REPLICATE(LEVEL,`ENDDO_LOOP')
      end')dnl
GENERIC_BROADCAST(0)
GENERIC_BROADCAST(1)
GENERIC_BROADCAST(2)
GENERIC_BROADCAST(3)
dnl GENERIC_BROADCAST(4)
dnl GENERIC_BROADCAST(5)
dnl GENERIC_BROADCAST(6)
dnl GENERIC_BROADCAST(7)
c
c that is all
c
