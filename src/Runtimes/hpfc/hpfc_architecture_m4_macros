dnl
dnl $RCSfile: hpfc_architecture_m4_macros,v $ version $Revision$
dnl ($Date: 1996/09/09 16:43:32 $, )
dnl
dnl DEFAULT ARCHITECTURE
dnl
ifdef(`PVM_ARCH',,`define(`PVM_ARCH',`syscmd(pvmgetarch)')')dnl
dnl
dnl MACHINE TYPE
dnl
define(`COM_PREFIX',`HPFC')dnl
define(`_SUN4')dnl
define(`_SUNMP')dnl
define(`_PGON')dnl
define(`_CM5')dnl
define(`_CRAY')dnl
define(`_RS6K')dnl
define(`_ALPHA')dnl
dnl
dnl
define(`_spawn',`$@')dnl
define(`_getpe',)dnl
dnl
dnl PECULIAR DEFINITIONS
dnl 
ifelse(PVM_ARCH,`CM5',`define(`COM_PREFIX',`CM')')dnl
ifelse(PVM_ARCH,`SUN4',`define(`_SUN4',`$@')',
       PVM_ARCH,`SUNMP',`define(`_SUNMP',`$@')', 
       PVM_ARCH,`PGON',`define(`_PGON',`$@')',
       PVM_ARCH,`CRAY',`define(`_CRAY',`$@')',
       PVM_ARCH,`CM5',`define(`_CM5',`$@')',
       PVM_ARCH,`ALPHA',`define(`_ALPHA',`$@')',
       PVM_ARCH,`RS6K',`define(`_RS6K',`$@')')dnl
dnl
dnl whether to spawn
dnl
ifelse(PVM_ARCH, `CRAY', `define(`_spawn',)')dnl
ifelse(PVM_ARCH, `CRAY', `define(`_getpe',`$@')')dnl
dnl
dnl GNU Fortran compiler g77
dnl byte is not supported...
dnl
ifdef(`USE_GNU',`define(`_GNU',`$@')',`define(`_GNU',)')dnl
ifdef(`USE_GNU',`define(`_NOGNU',)',`define(`_NOGNU',`$@')')dnl
dnl
dnl TO USE IBM PVMe IMPLEMENTATION
dnl 
ifdef(`USE_PVMe',`define(`_PVMe',`$@')',`define(`_PVMe',)')dnl
dnl
dnl THAT IS ALL
dnl
