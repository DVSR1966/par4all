#!/bin/csh -f
#
#  stf is a csh script to standardize the structure of a Fortran program
#  using ISTST.
#    (via the combined tool fragment ISTLY = ISTLX/ISTYP and then ISTST)
#
#
#  Invocation:
#
#  stf Fortran_source_file
#
#  Check command line validity.

set echo_err = /home/users/pips/Toolpack/toolpack1.2/util/echoerr

if ( $#argv < 1 ) then
	$echo_err Invocation:
	$echo_err ""
	$echo_err stf Fortran_source_file
	$echo_err ""
	$echo_err Structured code is sent to standard output
	$echo_err '(and may be redirected to a file).'
	$echo_err ""
	exit 1
endif

#  Check that Fortran_source_file exists.

if ( -e $1 == 0 ) then
	$echo_err "'$1' does not exist."
	exit 1
endif

#
#  Create PFS.  If PFS already exists, exit with an advisory message.
#

if ( -e _.TOOLPACK == 0 ) then
	mkdir _.TOOLPACK
else
	$echo_err Toolpack-created directory '"_.TOOLPACK"' exists.
	$echo_err Remove with script '"discard"'.
	exit 1
endif

#  Make a tab-free copy of the Fortran source and use it as source.
set src = ly.src$$
expand $1 > _.TOOLPACK/$src
#  Comment file name.
set cmt = _.lycmt
#  Parse tree file name.
set tree = _.lytree
#  Symbol table file name.
set symb = _.ypsymb
#  Comment index file name.
set indx = _.lyindx

#  Create the interprocess file IST.CMD and append parameters for ISTLY.
/home/users/pips/Toolpack/toolpack1.2/util/mkipf \
$src $cmt $tree $symb $indx

#
#  Invoke ISTLY.
#

/home/users/pips/Toolpack/toolpack1.2/exec/istly.u
if ($status != 0) then
	$echo_err "Unable to run istly.u"
	/bin/rm -rf _.TOOLPACK
	exit 1
endif

#
#  If tool terminated with errors, advise user to obtain listing.  Exit.
if ( `cat _.TOOLPACK/_.info` == -1 ) then
	$echo_err ""
	$echo_err Errors detected.
	$echo_err Invoke script '"getlst"' to obtain a listing showing
	$echo_err statement and token numbers.
	$echo_err ""
	/bin/rm -rf _.TOOLPACK
	exit
endif

#  Create the interprocess file IST.CMD and append parameters for ISTST.
/home/users/pips/Toolpack/toolpack1.2/util/mkipf \
$tree $symb $indx $cmt \#1 -

#
#  Invoke ISTST.
#

/home/users/pips/Toolpack/toolpack1.2/exec/istst.u
if ($status != 0) then
	echo "Unable to run istst.u"
	/bin/rm -rf _.TOOLPACK
	exit 1
endif

/bin/rm -rf _.TOOLPACK

#EOF
