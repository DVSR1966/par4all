# Optimization by unrolling of loop 100 and 200,
# and by repetitive partial evaluation

delete convol-unroll-C

# create convol-unroll-C ${PIPS_ROOT}/Validation/Transformations/convol-unroll.c
create convol-unroll-C convol-unroll.c

setproperty ABORT_ON_USER_ERROR TRUE

setproperty PRETTYPRINT_C_CODE TRUE
setproperty PRETTYPRINT_STATEMENT_NUMBER FALSE

setproperty FOR_TO_DO_LOOP_IN_CONTROLIZER TRUE

# for scalarization
#
setproperty ARRAY_PRIV_FALSE_DEP_ONLY FALSE
activate MUST_REGIONS

activate C_PARSER

# Show convolution function
#
module convol
display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol01.c

# Show main program
#
module main

display PRINTED_FILE
# shell cp convol-unroll-C.database/IMAGE_PROCESSING/IMAGE_PROCESSING.pref convol_MAIN.c

# Show interprocedural preconditions for module convol
#
module convol

activate INTERPROCEDURAL_SUMMARY_PRECONDITION
activate PRECONDITIONS_INTER_FULL 
activate TRANSFORMERS_INTER_FULL  

activate PRINT_CODE_PRECONDITIONS

display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.prec convol01P.c

activate PRINT_CODE

# Propagate constants (cloning could be used too)
#
apply PARTIAL_EVAL
display PRINTED_FILE 
# shell cp convol-unroll-C.database/convol/convol.pref convol02.c

# Unroll internal loops
#
apply FULL_UNROLL
l100

apply FULL_UNROLL
l200

display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol03.c

# Eliminate useless variables
#
apply USE_DEF_ELIMINATION
display PRINTED_FILE
#shell cp convol-unroll-C.database/convol/convol.pref convol04.c

# Simplify subscript expressions
#
apply PARTIAL_EVAL
display PRINTED_FILE 
#shell cp convol-unroll-C.database/convol/convol.pref convol05.c

# Increase locality: not a god idea in C but this shows PIPS works
#
apply LOOP_INTERCHANGE
l400
display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol06.c

# Reduce memory footprint
#
apply LOOP_TILING
l400
10 0
0 10
display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol07.c

# The code should be scalarized first...
#
apply SCALARIZATION
display PRINTED_FILE

# atomize
#
apply NEW_ATOMIZER
display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol08.c

# Loop invariant code motion
# SG: core dumps
#
#apply INVARIANT_CODE_MOTION
#display PRINTED_FILE
# shell cp convol-unroll-C.database/convol/convol.pref convol09.c

quit
