delete ${WSPACE}
setproperty ABORT_ON_USER_ERROR TRUE
setproperty FREIA_LABEL_ARCS=TRUE
#setenv PIPS_HWAC_DEBUG_LEVEL=5
#setenv PIPS_HWAC_DEBUG_LEVEL=7
create ${WSPACE} ${FILE} freia.c
shell ${PIPS_CC:-cc} -c ${FILE} -o ${WSPACE}.database/Tmp/${WSPACE}.o
activate C_PARSER
setproperty PRETTYPRINT_C_CODE TRUE
echo
echo ! initial function
echo
display PRINTED_FILE[${WSPACE}]
#
# apply compiler and clean unused images
#
apply FREIA_SPOC_COMPILER[${WSPACE}]
setproperty DYNAMIC_ALLOCATION="freia_common_create_data"
setproperty DYNAMIC_DEALLOCATION="freia_common_destruct_data"
apply CLEAN_UNUSED_DYNAMIC_VARIABLES[${WSPACE}]
touch SPOC_FILE[${WSPACE}]
touch PROPER_EFFECTS[${WSPACE}]
touch CUMULATED_EFFECTS[${WSPACE}]
setproperty ABORT_ON_USER_ERROR FALSE
#
# show spoc results
#
echo
echo ! graphs
echo
shell cat ./${WSPACE}.database/${WSPACE}/*.dot
echo
echo ! acceleration function
echo
display SPOC_FILE[${WSPACE}]
#
# new function code
#
echo
echo ! updated function
echo
display PRINTED_FILE[${WSPACE}]
close
delete ${WSPACE}
quit
